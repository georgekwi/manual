\chapter{ Use Case: XML flights to JSON }
As a developer I want to receive XML flight records and generate an enhanced JSON data structure with the corresponding flights.
\newline
The JSON data structure is enhanced as follows:
\begin{itemize}
\item It contains the corresponding appropriate JSON structure contain the flights from the XML inputs
\item It is enhanced with total seating capacity per flight
\item It is enhanced with prices adjusted across a number of currencies
\item It is enhanced such that important KPIs are contained at the same level as the flights
\end{itemize}
%%
%%
%%
\section{Organize DataWeave code with variables and functions}

%%%%%%
\subsection{Create a new flow}
\begin{enumerate}
\item Create a new Mule Configuration file and name it \ttt{mod2}, it will contain the solutions to all WTs from module 2.
\item Create a new flow named \ttt{mod2-flights}
\item Drop a DW to the process area of the flow
\item Define the payload input metadata to  \ttt{flights\_xml}
\item Edit the sample data
\item Turn on the preview
\item Change the output to \ttt{application/dw}
\item Change the body of the expression to
  \begin{verbatim}
    %dw 2.0
    output application/json
    ---
    payload..*return
  \end{verbatim}
\end{enumerate}

%%%%%%
\subsection{Create a variable}
\begin{enumerate}[resume*]
\item Create a variable visible throughout the DW expression
  \begin{verbatim}
    var theTotalSeats = 400
  \end{verbatim}
\item Add the \ttt{totalSeats} field to the existing list of objects, do it for a single object then do it for all objects in the collection
  \begin{verbatim}
    %dw 2.0
    output application/json
    var theTotalSeats = 400
    ---
    payload..*return[0] ++ {
      totalSeats: theTotalSeats
    }
  \end{verbatim}
\item Do it now for all elements
  \begin{verbatim}
    %dw 2.0
    output application/json
    var theTotalSeats = 400
    ---
    payload..*return map ($ ++ {
      totalSeats: theTotalSeats
    })
  \end{verbatim}
  \emph{
    \ttt{++} we have already seen when concatenating strings we see it operating with objects as well because it is overloaded, more on overloading soon.
  }
\item There is another way to add a field(s) to an existing object
  \begin{verbatim}
    %dw 2.0
    output application/json
    var theTotalSeats = 400
    ---
    payload..*return map {
      ($)
      totalSeats: theTotalSeats
    }
  \end{verbatim}
  \emph{
    We have already seen $\{()\}$ when eliminating arrays, here these $()$ are applied to single objects with the same effect; i.e. destroy the object and retrieve the basic building blocks of the object, that is the keys and the associated values.  These basic building blocks are then introduced in the new object created by the outermost object.}
  \newline
  \emph{
    Pick the method you prefer to concatenate objects, I prefer the latter which is the one I shall be using for the duration of this class.
  }
\end{enumerate}

%%%%%%
\subsection{Fix the Boing typo}
\begin{enumerate}[resume*]
\item Add another field containing the \ttt{planeType} value
  \begin{verbatim}
    %dw 2.0
    output application/dw
    var theTotalSeats=400
    ---
    payload..*return map {
      ($),
      planeType: $.planeType,
      totalSeats: theTotalSeats
    }
  \end{verbatim}
  \emph {
    A quick inspection of the output identifies two \ttt{planeType} fields containing the same value.  This is a problem because we are interested in a single field containing the corrected spelling for Boeing.  We should remove one of them and removing the first one is the right choice because this first one is the one we have no control over.  The second \ttt{planeType} is the one we have full control because we are adding it!
  }
\item Remove the first \ttt{planeType} field
  \begin{verbatim}
    %dw 2.0
    output application/dw
    var theTotalSeats=400
    ---
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType,
      totalSeats: theTotalSeats
    }
  \end{verbatim}
  \emph{
    This unary \ttt{-} operator is overloaded to also apply to objects and remove fields.  You can keep applying it successively if you want to remove multiple fields--e.g. \ttt{\$ - "planeType" - "price"} 
  }
\item Fix the \ttt{Boing} typo
  \begin{verbatim}
    %dw 2.0
    output application/dw
    var theTotalSeats=400
    ---
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: theTotalSeats
    }
  \end{verbatim}
  \emph{
    It is worth noting that \ttt{replace} and \ttt{with} are two separate functions where the result of \ttt{replace} is the first argument to \ttt{with}.  It makes the code ``talk to you in English''!  This is the beauty of infix function invocation. 
  }
\end{enumerate}

%%%%%%
\subsection{Calculate the total seats as a function of the \ttt{planeType} using \ttt{fun}}
\begin{enumerate}[resume*]
\item Create and apply a function and start unit-testing it
  \begin{verbatim}
    %dw 2.0
    output application/dw
    var theTotalSeats = 400
    fun getTotalSeats(pt) = pt
    ---
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeats($.planeType)
    }
  \end{verbatim}
  \emph{
    \ttt{pt} is a user defined arbitrary name, denoting the sole input parameter
  }
  \newline
  \emph{
    By unit testing we refer to the method by which small chunks of our functionality is tested before we put them all together.  We are not referring to automated/regression testing.
  }
\item Create the condition that identifies 737s over the other types of planes
  \begin{verbatim}
    %dw 2.0
    output application/dw
    var theTotalSeats = 400
    fun getTotalSeats(pt) = pt contains "737"
    ---
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeats($.planeType)
    }
  \end{verbatim}
\item Enclose the condition in an \ttt{if} expression
  \begin{verbatim}
    fun getTotalSeats(pt) = if (
      pt contains "737"
    ) 150 else 300
    ---
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeats($.planeType)
    }
  \end{verbatim}
\item Change the function expression to allow for the 727 and 707 to be set to 150 seats
  \begin{verbatim}
    fun getTotalSeats(pt) = if (
      pt contains "737" or
      pt contains "707" or
      pt contains "727"
    ) 150 else 300
  \end{verbatim}
\item Fix the error \ttt{Cannot coerce String (737) to Boolean}
  \begin{verbatim}
    fun getTotalSeats(pt) = if (
      (pt contains "737") or
      (pt contains "707") or
      (pt contains "727")
    ) 150 else 300
  \end{verbatim}
  \emph{
    Parenthesizing to enforce precedence is required in this context because \ttt{or} has higher precedence vs \ttt{contains}
  }
  \emph{
    A chunk of the issues you will have when you start writing DW expressions on your own will stem from precedence rules.
  }
\item Discuss issues with the \ttt{getTotalSeats} functions
  \begin{enumerate}
  \item We execute this function once per record
  \item We are searching strings
  \item We do this string search three times
  \item The function is not that efficient, we could do better
  \end{enumerate}
  \emph{
    Please do not think for a moment that in modern computing string searches as slow, they are fast and could
    be optimized in a number of ways.  Nonetheless, this discussion has merit in the presence of large to very
    large data sets where the function is called once per record; i.e. every little bit helps!
  }
\end{enumerate}

%%%%%%
\subsection{Calculate efficiently the total seats as a function of \ttt{planeType} using a lambda expression}
\begin{enumerate}[resume*]
\item Create another function named \ttt{getTotalSeatsL}
  \begin{verbatim}
    var getTotalSeatsL = (pt) -> pt
  \end{verbatim}
  \emph{
    L stands for Lambda, we store an anonymous function to a variable; i.e. we provide this anonymous function
    with a name.  Additionally, the body of this function evaluates into the argument we passed--this encourages
    unit testing.
  }
\item Apply the function to in the expression and get results
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType)
    }
  \end{verbatim}
  \emph{
    Applying the function as soon as possible and getting results as we further develop our function
    will only facilitate unit testing and code correctness.
  }
\item Get the last three characters of the plane type
  \begin{verbatim}
    var getTotalSeatsL = (pt) -> pt[-3 to -1] as Number
  \end{verbatim}
  \emph{
    We can now use the number to compare instead of doing a string search which will speed up the execution of
    our code.
  }
\item Introduce closures (i.e. localized declarations) using \ttt{do \{\}}
  \begin{verbatim}
    var getTotalSeatsL = (pt) -> do {
      var pn = pt[-3 to -1] as Number
      ---
      pn
    }
  \end{verbatim}
  \emph{
    A \href{https://en.wikipedia.org/wiki/Closure_(computer_programming)}{closure} is a construct that allows for
    the declaration of variables, functions, etc with a localized scope.  The \ttt{---} serve the same purpose
    like the \ttt{---} we see in other DW expressions, they are section separators used to separate the
    declarations and the expression.
  }
\item Add the conditional to the function
  \begin{verbatim}
    var getTotalSeatsL = (pt) -> do {
      var pn = pt[-3 to -1] as Number
      ---
      if (pn == 737 or pn == 707 or pn ==727) 150 else 300
    }    
  \end{verbatim}
  \emph{
    You can use either one of these two functions to calculate the total seats; however, if you would like to use
    features such as function overloading you MUST stick with the \ttt{fun}.
  }
\end{enumerate}

%%%%%%
\subsection{Adjust price for currency}
\begin{enumerate}[resume*]
\item Create an object that contains currency exchange rates
  \begin{verbatim}
    var xes = {
      USD: 1.0,
      EUR: 0.9,
      GBP: 0.8,
      CAD: 1.3,
      AUD: 1.5,
      MXN: 25,
      INR: 72
    }
  \end{verbatim}
  \emph{
    We hard-code these currencies because we are within the confines of training.  We can easily fetch these
    currencies dynamically from any data source and generate the map.
  }
\item Create a function to calculate the price for a currency
  \begin{verbatim}
    var adjustFor = (p,c) -> p * xes[c]
  \end{verbatim}
\item Apply the function in prefix syntax
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType),
      priceEUR: adjustFor($.price,"EUR")
    }
  \end{verbatim}
\item Apply the function in infix syntax
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType),
      priceEUR: adjustFor($.price,"EUR"),
      priceGBP: $.price adjustFor "GBP"
    }    
  \end{verbatim}
  \emph{
    Functions with exactly two arguments get this infix application support!  In fact, infix function
    application is encouraged because (1) it is natural in its application, (2) no need to use excessive
    parenthesis, and (3) allows for a more natural application of expression chains.
  }
\end{enumerate}

%%
%%
%%
\section{Reuse DataWeave transformations}

%%%%%%
\subsection{Store DW code in a file}
\begin{enumerate}
\item Switch to the XML view of your file
\item Navigate under the \ttt{mod2-flights} flow and illustrate how the code is inline
\item Switch back to the graphical view (aka Message Flow)
\item Go to the properties of the DW processor under the \ttt{mod2-flights}
\item Click the Edit current target button (pencil icon)
\item Click the radio button File and type \ttt{dw/transforms/mod2/flights} in the text field to the right
\item Click OK
  \emph {
    From the point of view of the DW properties UI nothing has changed.  Nonetheless, with this action we have stored the DW code inside a new file under \ttt{src/main/resources/dw/transforms/mod2} named \ttt{flights.dwl}
  }
\end{enumerate}

%%%%%%
\subsection{Reuse the DW code from the file}
\begin{enumerate}[resume*]
\item Create a new flow named \ttt{mod2-reuse-flights}
\item Drop a DW to the process area of the flow
\item Switch to the XML view
\item Locate the DW you just created
\item Remove the \ttt{CDATA} tag
  \begin{verbatim}
    <![CDATA[%dw 2.0 output application/java --- {}]]>
  \end{verbatim}
\item Introduce the \ttt{/} closing to the opening \ttt{<ee:set-payload />} tag, this should automatically remove the explicit closing tag
\item Add the attribute \ttt{resource} to the \ttt{<ee:set-payload />} tag
  \begin{verbatim}
    <ee:set-payload resource="dw/transforms/mod2/flights.dwl" />
  \end{verbatim}
  \emph{
    This is the only way you could reuse the full transformation, i.e. by modifying the XML.  Had you gone inside
    the UI and attempt to reuse the file, you would be overwriting it!  That pencil button is a "one way trip",
    only there to store the file not reference it.
  }
\item Switch back to the graphical view
\item Open the properties of the the DW processor under the \ttt{mod2-reuse-flights}
\item Turn on the Preview
  \emph{
    There is an issue that indicates that there is no metadata identifying what the payload is.  You can fix
    this issue by just setting the metadata again.  This issue is displayed because of DataSense.  This issue
    is only visible when in Studio, if we start the server and deploy our app, DataSense is never in play. 
  }
\item Set the input payload metadata to \ttt{flights\_xml}
\item Turn on the Preview and validate you see the result
\end{enumerate}


%%
%%
%%

\section{Create and use DataWeave modules}


%%%%%%
\subsection{Create a DW module}
\begin{enumerate}
\item Create a new folder(s) under \ttt{src/main/resources}
\item In the text field type \ttt{dw/modules}
\item Create a new file under \ttt{dw.modules} and name it \ttt{Currency.dwl}
\item Type on line 1
  \begin{verbatim}
    %dw 2.0
  \end{verbatim}
  \emph{
    DW modules can only contain declarations.  Declarations such as variables, function, types, etc.
  }
\item Navigate back to the DW processor under \ttt{mod2-reuse-flights}
\item Copy the \ttt{xes} variable and the \ttt{adjustFor} function
\item Paste to \ttt{Currency.dwl} under line 1 and save
\end{enumerate}

%%%%%%
\subsection{Use the module}
\begin{enumerate}[resume*]
\item Go back to the DW processor under \ttt{mod2-reuse-flights}
\item Use the module by fully qualifying the function to adjust the price for the CAD currency
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType),
      priceEUR: adjustFor($.price, "EUR"),
      priceGBP: $.price adjustFor "GBP",
      priceCAD: $.price dw::modules::Currency::adjustFor "CAD"
    }    
  \end{verbatim}
\item Import the new module below the output directive
  \begin{verbatim}
    import dw::modules::Currency
  \end{verbatim}
\item Use the module again this time by taking advantage of the \ttt{import} to adjust the price for AUD
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType),
      priceEUR: adjustFor($.price, "EUR"),
      priceGBP: $.price adjustFor "GBP",
      priceCAD: $.price dw::modules::Currency::adjustFor "CAD",
      priceAUD: $.price Currency::adjustFor "AUD"
    }      
  \end{verbatim}
\item Import the module again and provide an an alias to the module
  \begin{verbatim}
    import dw::modules::Currency as Curr
  \end{verbatim}
\item Use the module through the \ttt{Curr} alias next
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType),
      priceEUR: adjustFor($.price, "EUR"),
      priceGBP: $.price adjustFor "GBP",
      priceCAD: $.price dw::modules::Currency::adjustFor "CAD",
      priceAUD: $.price Currency::adjustFor "AUD",
      priceMXN: $.price Curr::adjustFor "MXN"
    }
  \end{verbatim}
\item Import all declarations to the current namespace
  \begin{verbatim}
    import * from dw::modules::Currency
  \end{verbatim}
\item Use directly the \ttt{adjustFor} function
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType),
      priceEUR: adjustFor($.price, "EUR"),
      priceGBP: $.price adjustFor "GBP",
      priceCAD: $.price dw::modules::Currency::adjustFor "CAD",
      priceAUD: $.price Currency::adjustFor "AUD",
      priceMXN: $.price Curr::adjustFor "MXN",
      priceINR: $.price adjustFor "INR"
    }
  \end{verbatim}
  \emph{
    There is also an inline version of the \ttt{adjustFor} function which takes precedence.
    As such we are not using the function provided by the module.  We can very easily
    verify by changing the body of the inline function, just change the body to 1000.
  }
\item Modify the last \ttt{import} to selectively import declarations and provide them with aliases
  \begin{verbatim}
    import adjustFor as adj4 from dw::modules::Currency
  \end{verbatim}
\item Use the \ttt{adj4} alias
  \begin{verbatim}
    payload..*return map {
      ($ - "planeType"),
      planeType: $.planeType replace /Boing/ with "Boeing",
      totalSeats: getTotalSeatsL($.planeType),
      priceEUR: adjustFor($.price, "EUR"),
      priceGBP: $.price adjustFor "GBP",
      priceCAD: $.price dw::modules::Currency::adjustFor "CAD",
      priceAUD: $.price Currency::adjustFor "AUD",
      priceMXN: $.price Curr::adjustFor "MXN",
      priceINR: $.price adj4 "INR"
    }
  \end{verbatim}
\end{enumerate}  

%%
%%
%%

\section{Optional: Calculate KPIs using the Arrays Module}


%%%%%%
\subsection{Inspect the Arrays module in the Documentation}
\begin{enumerate}
\item Navigate to the \href{https://docs.mulesoft.com/mule-runtime/4.3/dw-arrays}{Arrays module page} in the documentation
\item Visit the \ttt{countBy} function documentation and understand the signature
\item Visit the \ttt{every} function documentation and understand the signature
\item Visit the \ttt{some} function documentation and understand the signature
\item Visit the \ttt{sumBy} function documentation and understand the signature
\end{enumerate}

%%%%%%
\subsection{Add the KPI section}
\begin{enumerate}[resume*]
\item Add an object and set the record of flights to the object's \ttt{data} field
  \begin{verbatim}
    {
      data: payload..*return map {
        ($ - "planeType"),
        planeType: $.planeType replace /Boing/ with "Boeing",
        totalSeats: getPlaneTypeL($.planeType),
        priceEUR: adjustFor($.price,"EUR"),
        priceGBP: $.price adjustFor "GBP",
        priceCAD: $.price dw::modules::Currency::adjustFor "CAD",
        priceAUD: $.price Currency::adjustFor "AUD",
        priceMXN: $.price Curr::adjustFor "MXN",
        priceINR: $.price adj4 "INR"
      }
      
    }
  \end{verbatim}
\item Add an \ttt{kpi} field above the \ttt{data} field and assign an empty object to it
  \begin{verbatim}
    {
      kpi: {
        
      },
      data: payload..*return map {
        ($ - "planeType"),
        planeType: $.planeType replace /Boing/ with "Boeing",
        totalSeats: getPlaneTypeL($.planeType),
        priceEUR: adjustFor($.price,"EUR"),
        priceGBP: $.price adjustFor "GBP",
        priceCAD: $.price dw::modules::Currency::adjustFor "CAD",
        priceAUD: $.price Currency::adjustFor "AUD",
        priceMXN: $.price Curr::adjustFor "MXN",
        priceINR: $.price adj4 "INR"
      }
    }
  \end{verbatim}
  \emph{
    We will use this new field to store the calculated KPIs in subsequent steps of this WT.
  }
\end{enumerate}

%%%%%%
\subsection{Assert all flights are Delta operated}
\begin{enumerate}[resume*]
\item Import the \ttt{Arrays} module
  \begin{verbatim}
    import every,some,countBy,sumBy from dw::core::Arrays
  \end{verbatim}
\item Use the \ttt{every} function to assert whether all flights are Delta operated
  \begin{verbatim}
    kpi: {
      allDeltaFlights: payload..*return every (e) -> e.airlineName == "Delta"
    },
  \end{verbatim}
  \emph{
    You may want to refactor your code so that you execute \ttt{payload..*return} once.  You already have seen how to do it, consider it a bonus exercise.
  }
\end{enumerate}

%%%%%%
\subsection{Assert the existence of full flights}
\begin{enumerate}[resume*]
\item Use the \ttt{some} to assert whether there are any full flights
  \begin{verbatim}
    kpi: {
      allDeltaFlights: payload..*return every (e) -> e.airlineName == "Delta",
      anyFullFlights: payload..*return some ($.emptySeats == 0)
    },
  \end{verbatim}
  \emph{
    The result is \ttt{false}, i.e. there are no full flights.  However, the second to last flight is a full flight because the \ttt{emptySeats} tag is set to 0.  The problem lies with the comparison--we are comparing a string with a number.  You can either use a type cast to \ttt{\$.emptySeats as Number == 0} or the equality operator \ttt{\textasciitilde=} that autocasts its arguments.
  }
\item Use the operator \ttt{\textasciitilde=} to fix the \ttt{Boolean} condition
  \begin{verbatim}
    kpi: {
      allDeltaFlights: payload..*return every (e) -> e.airlineName == "Delta",
      anyFullFlights: payload..*return some ($.emptySeats ~= 0)
    },
  \end{verbatim}
\end{enumerate}

%%%%%%
\subsection{Calculate the number of full flights}
\begin{enumerate}[resume*]
\item Calculate the number of full flights
  \begin{verbatim}
    kpi: {
      allDeltaFlights: payload..*return every (e) -> e.airlineName == "Delta",
      anyFullFlights: payload..*return some ($.emptySeats ~= 0),
      fullFlightsCount: payload..*return countBy ($.emptySeats ~= 0)
    },
  \end{verbatim}
\end{enumerate}

%%%%%%
\subsection{Sum the total number of empty seats across all flights}
\begin{enumerate}[resume*]
\item Sum up all the \ttt{emptySeats} across all flights
  \begin{verbatim}
    kpi: {
      allDeltaFlights: payload..*return every (e) -> e.airlineName == "Delta",
      anyFullFlights: payload..*return some ($.emptySeats ~= 0),
      fullFlightsCount: payload..*return countBy ($.emptySeats ~= 0),
      totalEmptySeats: payload..*return sumBy $.emptySeats
    },    
  \end{verbatim}
  \emph{
    We should be seeing errors now.  These errors are false-positives (for the most part) due to DataSense.  You could potentially eliminate them by adding \ttt{payload..*return default []} to the right of every single \ttt{Array} function.  You can drill down by inspecting the signatures for one of the functions that does not raises the error vs the signature with a function that does, to gain more insight. 
  }
\end{enumerate}
